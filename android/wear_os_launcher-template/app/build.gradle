plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'io.github.aidenkoog.android_wear_os'
    compileSdk 33

    /* App signing key information */
    signingConfigs {
        aidenkoog {
            storeFile file('../keystore/android_aidenkoog_appkey.jks')
            storePassword "aidenkoog0000"
            keyAlias "android_aidenkoog"
            keyPassword "aidenkoog0000"
        }
    }

    defaultConfig {
        applicationId "io.github.aidenkoog.android_wear_os"
        minSdk 30
        targetSdk 33
        /*
         * -----------------------------------------------------------------------------------------
         * major.minor.hotfix description
         * 1) major (feature) : feature adding
         * 2) minor (major) : critical modification
         * 3) hotfix
         * -----------------------------------------------------------------------------------------
         */
        versionCode 100
        versionName "1.0.0"
    }

    /* ---------------------------------------------------------------------------------------------
     * Build type
     * 1) release
     * 2) debug
     * 3) stage
     * --------------------------------------------------------------------------------------------*/
    buildTypes {
        release {
            applicationIdSuffix ""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"\"")
            buildConfigField("String", "URL_TCP", "\"\"")
            manifestPlaceholders = [appLabel: "Android Wear OS App"]
        }
        debug {
            applicationIdSuffix ""
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"\"")
            buildConfigField("String", "URL_TCP", "\"\"")
            manifestPlaceholders = [appLabel: "Android Wear OS App DEV"]
        }
        stage {
            debuggable true
            applicationIdSuffix ""
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"\"")
            buildConfigField("String", "URL_TCP", "\"\"")
            manifestPlaceholders = [appLabel: "Android Wear OS App STAGE"]
        }
    }

    // Enable data binding option
    android.buildFeatures.dataBinding = true

    // Enable compose, viewBinding
    buildFeatures {
        viewBinding true
        compose true
    }

    // Compose compile option.
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.1"
    }

    // Kotlin options.
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.tiles.ExperimentalHorologistTilesApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.compose.tools.ExperimentalHorologistComposeToolsApi"
    }

    /*
     * This data is combined with the build type data.
     * As a result, you can see the build variant below.
     * 1) krDebug, 2) krStage, 3) krRelease
     */
    flavorDimensions "destination"
    productFlavors {
        kr {
            dimension "destination"
        }
    }

    // APK file's name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "AndroidWearOsApp"
            def SEP = "_"
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def date = new Date()
            def formattedDate = date.format('yyyyMMdd_HHmm')
            def newApkName = project + SEP + buildType.toUpperCase() + SEP + versionName + SEP + versionCode + SEP + formattedDate + ".apk"
            outputFileName = new File(newApkName)
        }
    }

    packagingOptions {
        exclude "META-INF/atomicfu.kotlin_module"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.wear:wear:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Constraint layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Rx-java
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.8.0"

    // Room
    implementation 'androidx.room:room-runtime:2.5.1'
    implementation 'androidx.room:room-rxjava2:2.5.1'
    kapt 'androidx.room:room-compiler:2.5.1'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.43.2"
    kapt "com.google.dagger:hilt-android-compiler:2.43.2"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    // Multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    // Logger
    implementation 'com.orhanobut:logger:2.2.0'

    // Lottie
    implementation 'com.airbnb.android:lottie:5.1.1'

    // Navigation UI
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.6.4"

    // Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    // Health
    implementation("androidx.health:health-services-client:1.0.0-beta03")

    // Bridge between futures and coroutines
    implementation 'com.google.guava:guava:30.1.1-android'
    implementation "androidx.concurrent:concurrent-futures-ktx:1.1.0"

    // Ongoing Activity
    implementation "androidx.wear:wear-ongoing:1.0.0"

    // Use to implement wear watchfaces
    implementation("androidx.wear.watchface:watchface:1.1.1")

    // Use to implement wear watchface complications
    implementation "androidx.wear.watchface:watchface-complications-data-source:1.1.1"
    // (Kotlin-specific extensions)
    implementation "androidx.wear.watchface:watchface-complications-data-source-ktx:1.1.1"

    // Use to implement a watchface style and complication editor
    implementation("androidx.wear.watchface:watchface-editor:1.1.1")

    // Can use to render complications.
    // This library is optional and watchfaces may have custom implementation for rendering
    // complications.
    implementation "androidx.wear.watchface:watchface-complications-rendering:1.1.1"

    // General compose dependencies
    implementation("androidx.activity:activity-compose:1.7.1")
    implementation("androidx.compose.ui:ui-tooling-preview:1.4.3")
    // Other compose dependencies

    // Compose for Wear OS Dependencies
    implementation("androidx.wear.compose:compose-material:1.1.2")

    // Foundation is additive, so you can use the mobile version in your Wear OS app.
    implementation("androidx.wear.compose:compose-foundation:1.1.2")

    // If you are using Compose Navigation, use the Wear OS version (NOT THE MOBILE VERSION).
    implementation("androidx.wear.compose:compose-navigation:1.1.2")

    // Testing
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.4.3")
    debugImplementation("androidx.compose.ui:ui-tooling:1.4.3")

    // Datastore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    // Tile
    implementation "androidx.wear.tiles:tiles-material:1.1.0"

    // Horologist
    implementation "com.google.android.horologist:horologist-compose-tools:0.2.7"
    implementation "com.google.android.horologist:horologist-tiles:0.2.7"

    // Image loading - compose
    implementation 'io.coil-kt:coil-compose:2.2.2'

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:26.5.0')

    // Firebase
    // Since BoM is used, there is no need to specify the version of Firebase items.
    // Declare the dependency for the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-core'

    // License
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'
}